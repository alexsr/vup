#version 450

layout (local_size_x = 128) in;

#include "../particle_util.inc.comp"

#include "../ssbos.inc.comp"

layout (std430, binding = 7) buffer densities {
    float scalar_new[];
};

layout (std430, binding = 9) buffer boundary_buffer {
    vec4 boundaries[];
};

void main() {
    uint id = gl_GlobalInvocationID.x;
    if (id >= N) {
        return;
    }
    Particle p = particles[id];
    p.density_adv = 0;
    for (int i = 0; i < neighbor_counter[id]; i++) {
        int j = neighbor[id * NEIGHBOR_AMOUNT + i];
        Particle p_other = particles[j];
        p.density_adv += p_other.mass * dot(p.vel - p_other.vel, demo_consts.kernel_grad
                         * cubic_grad(demo_consts.h, p.pos - p_other.pos));
    }
    for (int j = 0; j < B; j++) {
        float boundary_psi = boundaries[j].w;
        vec4 pij = vec4((p.pos - boundaries[j]).xyz, 0);
        if (dot(pij, pij) < demo_consts.h2) {
            p.density_adv += boundary_psi * demo_consts.kernel_grad
                             * dot(p.vel, cubic_grad(demo_consts.h, pij));
        }
    }
    p.density_adv = max(p.density_adv, 0.0);
    if (neighbor_counter[id] < 20) {
        p.density_adv = 0;
    }
    particles[id].density_adv = p.density_adv;
    scalar_new[id] = p.density_adv;
    particles[id].K_v = p.density_adv * p.alpha * demo_consts.inv_dt;
}
