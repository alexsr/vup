#version 450

layout (local_size_x = 128) in;

#include "../particle_util.inc.comp"

#include "../ssbos.inc.comp"

#include "../boundary.inc.comp"

#include "../../../data_structures/compact_grid.inc.comp"

//https://animation.rwth-aachen.de/media/papers/2018-EG-ImplicitVisco.pdf

void main() {
    uint id = gl_GlobalInvocationID.x;
    if (id >= N) {
        return;
    }
    Particle p = particles[id];
    int neighbor_count = 0;
    int boundary_neighbor_count = 0;
    int i = int((p.pos.x - grid_params.grid_min.x) / grid_params.cell_size.x);
    int j = int((p.pos.y - grid_params.grid_min.y) / grid_params.cell_size.y);
    int k = int((p.pos.z - grid_params.grid_min.z) / grid_params.cell_size.z);
    int max_x = min(i + 2, grid_params.cell_count.x);
    int max_y = min(j + 2, grid_params.cell_count.y);
    int max_z = min(k + 2, grid_params.cell_count.z);
    for (int x = max(0, i - 1); x < max_x; x++) {
        int x_counter_offset = x * grid_params.cell_count.y * grid_params.cell_count.z;
        for (int y = max(0, j - 1); y < max_y; y++) {
            int y_counter_offset = y * grid_params.cell_count.z;
            for (int z = max(0, k - 1); z < max_z; z++) {
                int counter_id = x_counter_offset + y_counter_offset + z;
                if (counter_id >= CELL_COUNT) {
                    continue;
                }
                int grid_offset = 0;
                int boundary_grid_offset = 0;
                if (counter_id > 0) {
                    grid_offset = cell_counter[counter_id - 1];
                    boundary_grid_offset = boundary_cell_counter[counter_id - 1];
                }
                int n = cell_counter[counter_id] - grid_offset;
                for (int o = 0; o < n; o++) {
                    if (neighbor_count < NEIGHBOR_AMOUNT) {
                        int other = grid[grid_offset + o];
                        if (id == other) {
                            continue;
                        }
                        Particle p_other = particles[other];
                        vec4 pij = vec4((p.pos - particles[other].pos).xyz, 0);
                        if (dot(pij, pij) < demo_consts.h2) {
                            neighbors[id * NEIGHBOR_AMOUNT + neighbor_count] = other;
                            neighbor_count++;
                        }
                    }
                }
                n = boundary_cell_counter[counter_id] - boundary_grid_offset;
                for (int o = 0; o < n; o++) {
                    if (boundary_neighbor_count < NEIGHBOR_AMOUNT) {
                        int other = boundary_grid[boundary_grid_offset + o];
                        vec4 pij = vec4((p.pos - boundaries[other].pos).xyz, 0);
                        if (dot(pij, pij) < demo_consts.h2) {
                            boundary_neighbors[id * NEIGHBOR_AMOUNT + boundary_neighbor_count] = other;
                            boundary_neighbor_count++;
                        }
                    }
                }
            }
        }
    }
    neighbors_counter[id] = neighbor_count;
    boundary_neighbors_counter[id] = boundary_neighbor_count;
}

