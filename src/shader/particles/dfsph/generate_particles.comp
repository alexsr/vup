#version 450

layout (local_size_x = 128) in;

#include "particle_util.inc.comp"

#include "ssbos.inc.comp"

ivec3 calc_particle_index_3D(uint id, uvec4 res) {
    uint temp = id % (res.x * res.y);
    return ivec3(temp % res.x, temp / res.x, id / (res.x * res.y));
}

struct Particle_gen_settings {
    uvec4 res;
    vec4 lower;
    float mass_scaling;
    float rest_density;
    float viscosity;
    float temperature;
    float heat_const;
    float latent_heat_max;
};

layout (std430, binding = 15) readonly buffer particle_settings_ubo {
    Particle_gen_settings gen_settings;
};

void main() {    
    uint id = gl_GlobalInvocationID.x;
    if (id >= N) {
        return;
    }
    Particle p;
    ivec3 pos_offset = calc_particle_index_3D(id, gen_settings.res);
    float pos_step = demo_consts.r * 2;
    float scaled_h = gen_settings.mass_scaling * demo_consts.h;
    p.pos = vec4(pos_offset.x * pos_step + gen_settings.lower.x + demo_consts.r,
                  pos_offset.y * pos_step + gen_settings.lower.y + demo_consts.r,
                  pos_offset.z * pos_step + gen_settings.lower.z + demo_consts.r, 1.0f);
    p.precond = mat4(0);
    p.residual = vec4(0);
    p.p = vec4(0);
    p.x = vec4(0);
    p.z = vec4(0);
    p.vel_diff = vec4(0);
    p.vel = vec4(0);
    p.accel = vec4(0);
    p.mass = gen_settings.rest_density * scaled_h * scaled_h * scaled_h;
    p.rest_density = gen_settings.rest_density;
    p.density = gen_settings.rest_density;
    p.density_adv = 0.0f;
    p.alpha = 1.0f;
    p.K = 0.0f;
    p.K_v = 0.0f;
    p.viscosity = gen_settings.viscosity;
    p.temperature = gen_settings.temperature + 273.15f;
    p.heat_const = gen_settings.heat_const;//0.591f / 4181.3f; // 0.00143f;
    p.heat_buffer = gen_settings.latent_heat_max;
    particles[id] = p;
}
