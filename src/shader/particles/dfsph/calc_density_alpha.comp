#version 450

layout (local_size_x = 128) in;

#include "particle_util.inc.comp"

#include "ssbos.inc.comp"

#include "boundary.inc.comp"

#include "../../data_structures/compact_grid.inc.comp"

void main() {    
    uint id = gl_GlobalInvocationID.x;
    if (id >= N) {
        return;
    }
    Particle p = particles[id];
    p.density = p.mass * demo_consts.kernel * cubic(demo_consts.h, 0.0);
    vec4 density_grad_sum = vec4(0.0f);
    float density_length_sum = 0.0f;
    for (int i = 0; i < neighbors_counter[id]; i++) {
        int j = neighbors[id * NEIGHBOR_AMOUNT + i];
        Particle p_other = particles[j];
        vec4 pij = vec4((p.pos - p_other.pos).xyz, 0);
        p.density += p_other.mass * demo_consts.kernel * cubic(demo_consts.h, pij);
        vec4 density_grad = p_other.mass * demo_consts.kernel_grad * cubic_grad(demo_consts.h, pij);
        density_grad_sum += density_grad;
        density_length_sum += dot(density_grad, density_grad);
    }
    for (int i = 0; i < boundary_neighbors_counter[id]; i++) {
        int j = boundary_neighbors[id * NEIGHBOR_AMOUNT + i];
        float boundary_psi = boundaries[j].pos.w;
        vec4 pij = vec4((p.pos - boundaries[j].pos).xyz, 0);
        p.density += boundary_psi * demo_consts.kernel * cubic(demo_consts.h, pij);
        vec4 density_grad = boundary_psi * demo_consts.kernel_grad * cubic_grad(demo_consts.h, pij);
        density_grad_sum += density_grad;
        density_length_sum += dot(density_grad, density_grad);
    }
    density_length_sum += dot(density_grad_sum, density_grad_sum);
    density_length_sum = max(density_length_sum, demo_consts.eps);
    particles[id].density = max(p.density, demo_consts.eps);
    p.alpha = -1.0f / density_length_sum;
    particles[id].alpha = p.alpha;
}
