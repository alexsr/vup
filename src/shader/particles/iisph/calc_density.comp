#version 450

layout (local_size_x = 128) in;

#include "particles.inc.comp"

layout (std430, binding = 3) buffer particles {
    Fluid_particle p[N];
};

layout (std430, binding = 4) buffer demo_constants{
    SPH_demo_constants demo_consts;
};

layout (std430, binding = 5) buffer neighborhood_data {
    int neighbor[NEIGHBOR_ARRAY_SIZE];
};

layout (std430, binding = 6) buffer neighborhood_count {
    int neighbor_counter[N];
};

void main() {    
    uint id = gl_GlobalInvocationID.x;
    if (id >= N) {
        return;
    }
    float density = p[id].mass * demo_consts.kernel * cubic(demo_consts.h, 0.0);
    for (int i = 0; i < neighbor_counter[id]; i++) {
        int j = neighbor[id * NEIGHBOR_AMOUNT + i];
        density += p[j].mass * demo_consts.kernel * cubic(demo_consts.h, distance(p[id].pos.xyz, p[j].pos.xyz));
    }
    p[id].density = density;
}
